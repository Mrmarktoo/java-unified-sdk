apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "cn.leancloud.sample"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    signingConfigs {
        config {
            keyAlias 'sign_sample'
            keyPassword '654321'
            storeFile file('demo-key.keystore')
            storePassword '123456'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }
        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java {
                exclude 'src/main/assets'
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(':storage-android')

    implementation "io.reactivex.rxjava2:rxandroid:${rootProject.ext.rxandroidVersion}"

    implementation("cn.leancloud:storage-core:${rootProject.ext.sdkVersion}") {
        exclude group: 'com.alibaba', module: 'fastjson'
        exclude group: 'org.ligboy.retrofit2', module: 'converter-fastjson'
    }
    implementation "com.alibaba:fastjson:${rootProject.ext.fastjsonVersion}"
    implementation "org.ligboy.retrofit2:converter-fastjson-android:${rootProject.ext.converterVersion}"

    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'junit:junit:4.12'
}

task renameFile {
    File assetsDir = file 'src/main/assets';
    FileCollection collection = files { assetsDir.listFiles() }
    collection.each {
        if (it.name.indexOf('java') > 0) {
            def name = it.name.substring(0, it.name.indexOf('.'))
            def newFile = 'src/main/assets/' + name;
            file(it).renameTo(file(newFile))
        }
    }
}

task copySource(type: Copy) {
    from 'src/main/java/cn/leancloud/sample/testcase'
    into 'src/main/assets'
    rename { String fileName ->
        fileName.replace('java', 'file')
    }
}

preBuild.dependsOn copySource
